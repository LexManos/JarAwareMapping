plugins {
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    //id 'com.github.ben-manes.versions' version '0.49.0'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'net.minecraftforge.gradleutils'
    
    group 'net.minecraftforge.lex'
    
    gradleutils.gitRoot = rootProject.layout.projectDirectory.dir('.git')
    version gradleutils.tagOffsetVersion
    println("$name: $version")

    repositories {
        mavenCentral()
        //mavenLocal()
        maven gradleutils.forgeMaven
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(17)
        withSourcesJar()
    }
    
    changelog {
        fromBase()
    }
    
    tasks.named('createChangelog').configure {
        gitDirectory = rootProject.layout.projectDirectory.dir('.git')
    }

    dependencies {
        implementation(libs.noexception)
        implementation(libs.slf4j.api)
        runtimeOnly   (libs.slf4j.simple)
        implementation(libs.guava)
        implementation(libs.bundles.asm)
        
        if (file('src/test/').exists()) {
            testImplementation(libs.junit.api)
            testRuntimeOnly(libs.bundles.junit.runtime)
        }
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications.register('mavenJava', MavenPublication) {
            from components.java

            pom {
                name = project.name
                //url = 'https://github.com/LexManos/JarAwareMapping'
                gradleutils.pom.setGitHubDetails(pom, 'LexManos', 'JarAwareMapping')
                
                license gradleutils.pom.licenses.MIT
                
                developers {
                    developer gradleutils.pom.Developers.OrionDevelopment
                    developer gradleutils.pom.Developers.LexManos
                }
            }
        }
        
        repositories {
            maven gradleutils.publishingForgeMaven
        }
    }
}
