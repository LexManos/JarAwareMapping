pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            library('guava',       'com.google.guava:guava:31.0.1-jre')
            library('noexception', 'com.machinezoo.noexception:noexception:1.7.1')
            library('srgutils',    'net.minecraftforge:srgutils:0.5.10')
            library('gson',        'com.google.code.gson:gson:2.9.0')
            library('java-diff',   'io.github.java-diff-utils:java-diff-utils:4.11')
            library('jopt-simple', 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3')
            library('markdowngenerator', 'net.steppschuh.markdowngenerator:markdowngenerator:1.3.1.1')

            version('asm', '9.7')
            library('asm',         'org.ow2.asm', 'asm'        ).versionRef('asm')
            library('asm-commons', 'org.ow2.asm', 'asm-commons').versionRef('asm')
            library('asm-tree',    'org.ow2.asm', 'asm-tree'   ).versionRef('asm')
            library('asm-util',    'org.ow2.asm', 'asm-util'   ).versionRef('asm')
            bundle('asm', ['asm', 'asm-commons', 'asm-tree', 'asm-util'])
            
			version('junit', '5.10.1')
			library('junit-api',               'org.junit.jupiter', 'junit-jupiter-api'   ).versionRef('junit')
			library('junit-engine',            'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
			library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.10.1')
			bundle('junit-runtime', ['junit-engine', 'junit-platform-launcher'])
            
            version('slf4j', '1.7.30')
            library('slf4j-api',    'org.slf4j', 'slf4j-api'   ).versionRef('slf4j')
            library('slf4j-simple', 'org.slf4j', 'slf4j-simple').versionRef('slf4j')
        }
    }
}

rootProject.name = 'JarAwareMapping'

include(':jam')
include(':jam-mcpconfig')
include(':jam-spi')
include(':jam-runtime')

project(':jam').projectDir = new File('jam')
project(':jam-mcpconfig').projectDir = new File('mcpconfig')
project(':jam-spi').projectDir = new File('spi')
project(':jam-runtime').projectDir = new File('runtime')